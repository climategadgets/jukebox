plugins {
    id 'com.github.spotbugs'
    id 'net.ltgt.errorprone' apply false
    id 'org.sonarqube'
}

sonarqube {
    properties {
        property "sonar.projectKey", "climategadgets_jukebox"
        property "sonar.organization", "climategadgets"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}
subprojects {

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'
    apply plugin: 'net.ltgt.errorprone'

    sourceCompatibility = 11
    targetCompatibility = 11

    group = 'com.homeclimatecontrol.jukebox'
    version = '8.0.1-SNAPSHOT'

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    jacoco {
        toolVersion = "0.8.6"
    }

    jacocoTestReport {
        reports {
            xml.required = true
            html.required = true
        }
    }

    spotbugs {

        excludeFilter = file("$rootProject.projectDir/findbugs-exclude.xml")
        ignoreFailures = true
    }

    repositories {

        mavenCentral()
    }

    dependencies {

        implementation 'org.apache.logging.log4j:log4j-api:2.14.1'
        implementation 'org.apache.logging.log4j:log4j-core:2.14.1'

	testImplementation 'junit:junit:4.13'

        testImplementation 'org.mockito:mockito-core:3.11.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
        testImplementation('org.assertj:assertj-core:3.20.2')

        errorprone 'com.google.errorprone:error_prone_core:2.14.0'
    }

    publishing {
        publications {
            maven(MavenPublication) {
            from components.java
            }
        }
    }
    test {
        useJUnitPlatform()
    }
}